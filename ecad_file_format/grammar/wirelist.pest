file = { SOI ~ "Wire List" ~ component_list ~ wire_list ~ EOI }

component_list = ${ "<<< Component List >>>" ~ nl ~ component* }
component      = ${
    component_name ~ maybe_space ~ #designator = string_space5 ~ space ~
    #footprint = string_space5 ~ nl
}
component_name    = ${ (!(" "{5, } | nl) ~ ANY){1, 40} }
string_space5  = ${ (!(" "{5, } | nl) ~ ANY)+ }
space          = _{ " "+ }

wire_list   = ${ "<<< Wire List >>>" ~ (!"[" ~ ASCII)+ ~ net* }
net         = ${ "[" ~ net_index ~ "]" ~ " " ~ net_name ~ nl ~ connection* ~ nl }
net_index   = ${ ASCII_DIGIT+ }
net_name    = ${ (!nl ~ ASCII)+ }
connection  = ${
    (!ASCII_ALPHA_UPPER ~ " ")+ ~ #designator = string_space5 ~ space ~
    #pin_id = string_space5 ~ space ~ pin_name ~ maybe_space ~
    io_type ~ maybe_space ~ #part_number = string_space5 ~ nl
}
pin_name    = ${ (!(" "{5, } | nl) ~ ASCII){1, 15} }
maybe_space = _{ " "* }
io_type     =  { "PASSIVE" | "OUTPUT" | "INPUT" | "I/O" | "OPEN COLLECTOR" | "OPEN EMITTER" | "POWER" }

nl = _{ "\n" | "\r\n" }
WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
