file = { SOI ~ "Wire List" ~ component_list ~ wire_list ~ EOI }

component_list = ${ "<<< Component List >>>" ~ "\n" ~ component* }
component      = ${
    #part_number = string_space5 ~ space ~ #designator = string_space5 ~ space ~
    #footprint = string_space5 ~ ("\n" | "\r\n")
}
string_space5  = ${ (!(" "{5, } | "\n" | "\r\n") ~ ASCII)+ }
space          = _{ " "+ }

wire_list   = ${ "<<< Wire List >>>" ~ (!"[" ~ ASCII)+ ~ net* }
net         = ${ "[" ~ net_index ~ "]" ~ " " ~ net_name ~ "\n" ~ connection* ~ "\n" }
net_index   = ${ ASCII_DIGIT+ }
net_name    = ${ (!"\n" ~ ASCII)+ }
connection  = ${
    (!ASCII_ALPHA_UPPER ~ " ")+ ~ #designator = string_space5 ~ space ~
    #pin_id = string_space5 ~ space ~ pin_name ~ maybe_space ~
    io_type ~ maybe_space ~ #part_number = string_space5 ~ "\n"
}
pin_name    = ${ (!(" "{5, } | "\n" | "\r\n") ~ ASCII){1, 15} }
maybe_space = _{ " "* }
io_type     =  { "PASSIVE" | "OUTPUT" | "INPUT" | "I/O" | "OPEN COLLECTOR" | "OPEN EMITTER" | "POWER" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
